; Процедура принимает в стеке число, указатель на буфер,
; символ добивки слева или #0, если без добивки
INT2STR:
        ; Сохранение регистров
        MOV	R0,-(SP)
        MOV	R1,-(SP)
        MOV	R2,-(SP)
        MOV	R3,-(SP)
        MOV	R4,-(SP)
        MOV	R5,-(SP)

        ; Извлечение аргументов
        ADD	#16,SP
        MOV     (SP)+,R2
        MOV     (SP)+,R1
        MOV     (SP),R0
        SUB	#22,SP

        MOV #0,-(SP)      ; Запись в стек значений степени 10
        MOV #12,-(SP)     ; последний ноль - это стоп алгоритму
        MOV #144,-(SP)
        MOV #1750,-(SP)
        MOV #23420,-(SP)
         
        MOV	#0,R4
M1:
        MOV     (SP)+,R5  ; Извлекаем степень 10
        CMP	R5,#0     ; Если конец, то идем к последнему шагу
        BEQ     D1
        MOV	#0,R3     ; Счетчик текущего разряда
C1:     CMP	R0,R5     ; Если текущее число меньше разряда, то сразу в запись
        BLT     S1
        SUB	R5,R0     ; ВЫчитаем текущую степень из числа
        INC	R3        ; Увеличиваем разряд
        JMP     C1

S1:	ADD 	R3,R4
	CMP	R4,#0
	BEQ	NO_SET_ZERO

     	ADD	#60,R3    ; Преобразуем число в десятичный вид и пишем в буфер
        MOVB	R3,(R1)+
	
        JMP     M1

NO_SET_ZERO:
        CMP	R2,#0    ; Если символ добивки равен нулю, то не добиваем
        BEQ	M1

        MOVB	R2,(R1)+

        JMP     M1

D1:     ADD	#60,R0    ; Для единицы, её просто нужно перевести и записать
        MOVB	R0,(R1)+
        
        MOVB	#0,(R1)   ; Ставим терминальный ноль

        ; Восстановление регистров
	MOV     (SP)+,R5
	MOV     (SP)+,R4
	MOV     (SP)+,R3
	MOV     (SP)+,R2
	MOV     (SP)+,R1
	MOV     (SP)+,R0

        RTS PC
